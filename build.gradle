// bintray plugin is used to upload artifacts to a bintray repo
plugins { id "com.jfrog.bintray" version "1.0" }

apply plugin: 'java'
apply from: 'http://dl.bintray.com/content/shemnon/javafx-gradle/8.1.1/javafx.plugin'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'org.hildan'
version = '0.1'
sourceCompatibility = '1.8'

repositories { mavenCentral() }

dependencies {
    testCompile 'junit:junit:4.+'
}

javafx {

    profiles {
        windows {
            category = 'Games'
        }
    }

    // Application name and ID presented by target OS
    appID 'CandyBoxCheater'
    appName 'CandyBox Cheater'
    vendor = "Hildan Software"

    // Main class of application
    mainClass 'org.hildan.candyboxcheater.Main'

    // deploy/info attributes
    category = 'Games'
    copyright = 'Copyright (c) 2016 Joffrey Bion'
    description = 'A JavaFX program that helps editing CandyBox2 saves.'
    licenseType = 'MIT'
    installSystemWide = true
    menu = true
    shortcut = true
}

bintray {
    user = project.bintrayUser // taken from gradle.properties
    key = project.bintrayApiKey // taken from gradle.properties
    configurations = ['archives']

    publish = true // the version should be auto published after an upload

    pkg {
        repo = project.bintrayRepoApps // taken from gradle.properties
        name = project.name
        desc = 'A JavaFX program that helps editing CandyBox2 saves.'
        labels = ['candybox', 'candybox2', 'cheat', 'save']

        String githubRepoName = project.name
        websiteUrl = 'https://github.com/joffrey-bion/' + githubRepoName
        issueTrackerUrl = 'https://github.com/joffrey-bion/' + githubRepoName + '/issues'
        vcsUrl = 'https://github.com/joffrey-bion/' + githubRepoName + '.git'

        licenses = ['MIT']
        version { gpg { sign = true } }
    }
}